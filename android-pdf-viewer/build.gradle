apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 1
        versionName "1.0.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'androidx.core:core:1.7.0'
    api 'com.github.mhiew:pdfium-android:1.9.2'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.github.metehan-gokbel'
                artifactId = 'android-pdf-viewer'
                version = '1.0.0'

                // Generates a pom.xml with the necessary information
                pom {
                    name = 'AndroidPdfViewer'
                    description = 'A library to display PDF files on Android'
                    url = 'https://github.com/metehan-gokbel/AndroidPdfViewer'

                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'metehan-gokbel'
                            name = 'Metehan GÃ¶kbel'
                        }
                    }

                    scm {
                        connection = 'scm:git:https://github.com/metehan-gokbel/AndroidPdfViewer.git'
                        developerConnection = 'scm:git:ssh:github.com:metehan-gokbel/AndroidPdfViewer.git'
                        url = 'https://github.com/metehan-gokbel/AndroidPdfViewer'
                    }
                }
            }
        }
    }
}
